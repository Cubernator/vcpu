// program rules

program = { SOI ~ data ~ instructions ~ EOI }

// common rules

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "#" ~ ( !NEWLINE ~ ANY )* ~ (NEWLINE | EOI) }

token_sep = _{
    ( WHITESPACE* ~ (COMMENT ~ WHITESPACE*)+ ) |
    WHITESPACE+
}

sign = { "+" | "-" }

dec_uint = @{ ASCII_DIGIT+ }
dec_int = @{ sign? ~ dec_uint }

bin_lit = @{ ASCII_BIN_DIGIT+ }
oct_lit = @{ ASCII_OCT_DIGIT+ }
hex_lit = @{ ASCII_HEX_DIGIT+ }

bin_uint = ${ "0b" ~ bin_lit }
oct_uint = ${ "0o" ~ oct_lit }
hex_uint = ${ "0x" ~ hex_lit }

uint = { bin_uint | oct_uint | hex_uint | dec_uint }
int = { bin_uint | oct_uint | hex_uint | dec_int }

exp = @{ ^"e" ~ int }
float = @{ dec_int ~ (("." ~ dec_uint? ~ exp?) | exp) }

// TODO: float literals

underscore = _{ "_" }

identifier = @{ ( ASCII_ALPHA | underscore ) ~ ( ASCII_ALPHANUMERIC | underscore )* }

label = { identifier ~ ":" }

// data rules

int_list = !{ int ~ ( "," ~ int)* }

data_block = ${ ".block" ~ token_sep ~ uint }
data_byte = ${ ".byte" ~ token_sep ~ int_list }
data_short = ${ ".short" ~ token_sep ~ int_list }
data_word = ${ ".word" ~ token_sep ~ int_list }

// TODO: string data
// TODO: float data

data_element = {
    data_block |
    data_byte  |
    data_short |
    data_word
}

labeled_data_element = !{ label? ~ data_element }

data = ${ ".data" ~ token_sep ~ labeled_data_element? ~ (token_sep ~ labeled_data_element)* }

// instruction rules

jump_target = { int | identifier }

register = ${ "$" ~ register_id }

instruction_alu = { mnemonic_alu ~ register ~ "," ~ register ~ "," ~ register }
instruction_flop = { mnemonic_flop ~ register ~ "," ~ register ~ "," ~ register }
instruction_i = { mnemonic_i ~ register ~ "," ~ register ~ "," ~ int }
instruction_iu = { mnemonic_iu ~ register ~ "," ~ register ~ "," ~ uint }
instruction_ds = { mnemonic_ds ~ register ~ "," ~ register }
instruction_li = { mnemonic_li ~ register ~ "," ~ int }
instruction_si = { mnemonic_si ~ register ~ "," ~ uint }
instruction_e = { mnemonic_e }
instruction_br = { mnemonic_br ~ register ~ "," ~ jump_target }
instruction_jr = { mnemonic_jr ~ register }
instruction_ls = { mnemonic_ls ~ register ~ "," ~ int ~ "(" ~ register ~ ")" }
instruction_j = { mnemonic_j ~ jump_target }

// shorthand instructions

instruction_push = { ^"PUSH" ~ register }
instruction_pop = { ^"POP" ~ register }
instruction_lwi = { ^"LWI" ~ register ~ "," ~ int }
instruction_lda = { ^"LDA" ~ register ~ "," ~ identifier }
instruction_lia = { ^"LIA" ~ register ~ "," ~ identifier }

instruction = {
    instruction_alu  |
    instruction_flop |
    instruction_i    |
    instruction_iu   |
    instruction_ds   |
    instruction_li   |
    instruction_si   |
    instruction_e    |
    instruction_br   |
    instruction_jr   | 
    instruction_ls   |
    instruction_j    |
    instruction_push |
    instruction_pop  |
    instruction_lwi  |
    instruction_lda  |
    instruction_lia
}

labeled_instruction = !{ label? ~ instruction }

instructions = ${ ".instructions" ~ token_sep ~ labeled_instruction? ~ (token_sep ~ labeled_instruction)* }

// enum rules

register_id = {
    ^"ZERO" |
    ^"V0"   |
    ^"V1"   |
    ^"A0"   |
    ^"A1"   |
    ^"A2"   |
    ^"A3"   |
    ^"A4"   |
    ^"T0"   |
    ^"T1"   |
    ^"T2"   |
    ^"T3"   |
    ^"T4"   |
    ^"T5"   |
    ^"T6"   |
    ^"T7"   |
    ^"T8"   |
    ^"T9"   |
    ^"S0"   |
    ^"S1"   |
    ^"S2"   |
    ^"S3"   |
    ^"S4"   |
    ^"S5"   |
    ^"S6"   |
    ^"S7"   |
    ^"S8"   |
    ^"S9"   |
    ^"SP"   |
    ^"FP"   |
    ^"RM"   |
    ^"RA"  
}

mnemonic_alu = {
    ^"ADD"  |
    ^"SUB"  |
    ^"MUL"  |
    ^"DIV"  |
    ^"AND"  |
    ^"OR"   |
    ^"XOR"  |
    ^"SLL"  |
    ^"SRL"  |
    ^"SRA"  |
    ^"SEQ"  |
    ^"SNE"  |
    ^"SLT"  |
    ^"SGT"  |
    ^"SLE"  |
    ^"SGE"  |
    ^"SLTU" |
    ^"SGTU" |
    ^"SLEU" |
    ^"SGEU"
}

mnemonic_flop = {
    ^"FADD" |
    ^"FSUB" |
    ^"FMUL" |
    ^"FDIV" 
}

mnemonic_i = {
    ^"ADDI"  |
    ^"SUBI"  |
    ^"MULI"  |
    ^"DIVI"  |
    ^"ANDI"  |
    ^"ORI"   |
    ^"XORI"  |
    ^"SLLI"  |
    ^"SRLI"  |
    ^"SRAI"  |
    ^"SEQI"  |
    ^"SNEI"  |
    ^"SLTI"  |
    ^"SGTI"  |
    ^"SLEI"  |
    ^"SGEI"
}

mnemonic_iu = {
    ^"SLTUI" |
    ^"SGTUI" |
    ^"SLEUI" |
    ^"SGEUI"
}

mnemonic_ds = {
    ^"COPY" |
    ^"FLIP" |
    ^"ITOF" |
    ^"FTOI"
}

mnemonic_li = {
    ^"LI" |
    ^"LHI"
}

mnemonic_si = {
    ^"SLO" |
    ^"SHI"
}

mnemonic_e = {
    ^"NOP" |
    ^"HALT"
}

mnemonic_br = {
    ^"BEZ" |
    ^"BNZ"
}

mnemonic_jr = {
    ^"JR" |
    ^"JLR"
}

mnemonic_ls = {
    ^"LB" |
    ^"LH" |
    ^"LW" |
    ^"SB" |
    ^"SH" |
    ^"SW"
}

mnemonic_j = {
    ^"JMP" |
    ^"JL"
}
